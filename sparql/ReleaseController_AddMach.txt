PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX factory: <https://w3id.org/ontoeng/factory#>
PREFIX osph: <https://w3id.org/ontoeng/osph#>
PREFIX cs: <https://w3id.org/ontoeng/controlSystem#>
PREFIX fsm: <http://www.learninglab.de/~dolog/fsm/fsm.owl#>
PREFIX ifcext: <https://w3id.org/ontoeng/IFC4_ADD1_extension#>


INSERT { 
	GRAPH ?graph { 
		?contrStMNew a owl:NamedIndividual , fsm:StateMachine .
		?controller osph:hasStateMachine ?contrStMNew .
			
		?contrStM fsm:contains ?contrRegion .
		?contrStMNew fsm:contains ?contrRegion .
		
		?contrRegion a owl:NamedIndividual , fsm:Region .
		?trRelIdle a owl:NamedIndividual , fsm:Transition .
		?trIdleRel a owl:NamedIndividual , cs:ReleaseTransition .
		?idleSt a owl:NamedIndividual , cs:RControllerIdleState .
		?relSt a owl:NamedIndividual , cs:RControllerReleaseState .
		?guardTrIdleRel a owl:NamedIndividual , cs:ReleaseGuard .
		?condTrIdleRel a owl:NamedIndividual , cs:ReleaseCondition .
		?entryAction a owl:NamedIndividual , osph:EventGeneration .
		?startOp a owl:NamedIndividual , factory:MachineToolStartOperation .
		
		?contrRegion fsm:hasStateMachineElement ?trRelIdle ,
			?trIdleRel ,
			?idleSt ,
			?relSt ,
			?guardTrIdleRel ,
			?condTrIdleRel ,
			?entryAction ,
			?startOp .
		
		?trRelIdle fsm:Source ?relSt .
		?trRelIdle fsm:Target ?idleSt .
		?trIdleRel fsm:Source ?idleSt .
		?trIdleRel fsm:Target ?relSt .
		
		?trIdleRel fsm:TransitionGuard ?guardTrIdleRel .
		?guardTrIdleRel fsm:GuardCondition ?condTrIdleRel .
		
		?relSt fsm:Entry ?entryAction .
		?entryAction osph:generatesEvent ?startOp .
		
		?eventType ifcext:declaresObject ?startOp .
	}
}
WHERE {
	?controller rdf:type cs:ReleaseController .
	OPTIONAL {
		?controller osph:hasStateMachine ?contrStM .
	}	
	#?machine osph:hasStateMachine/fsm:contains/fsm:hasStateMachineElement*/fsm:GuardEvent ?eventType .
	?machine osph:hasStateMachine/fsm:contains/fsm:hasStateMachineElement* ?trIdleWork .
	?trIdleWork fsm:Target ?workstate .
	?trIdleWork fsm:TransitionGuard/fsm:GuardEvent ?eventType .
	{?eventType rdf:type factory:MachineToolStartOperationType .}
	UNION {?eventType rdf:type factory:MachineToolStartOperation .}
	
	BIND(IF(bound(?contrStM),?null ,URI(CONCAT(STR(?controller),"_stM")) ) AS ?contrStMNew ) .  
	BIND(URI(CONCAT(STR(?controller),"_",STRAFTER(STR(?workstate),"#"),"_reg")) AS ?contrRegion ) .  
	BIND(URI(CONCAT(STR(?controller),"_",STRAFTER(STR(?workstate),"#"),"_trRelIdle")) AS ?trRelIdle ) .  
	BIND(URI(CONCAT(STR(?controller),"_",STRAFTER(STR(?workstate),"#"),"_idleSt")) AS ?idleSt ) .  
	BIND(URI(CONCAT(STR(?controller),"_",STRAFTER(STR(?workstate),"#"),"_relSt")) AS ?relSt ) .  
	BIND(URI(CONCAT(STR(?controller),"_",STRAFTER(STR(?workstate),"#"),"_guardTrIdleRel")) AS ?guardTrIdleRel ) .  
	BIND(URI(CONCAT(STR(?controller),"_",STRAFTER(STR(?workstate),"#"),"_trIdleRel")) AS ?trIdleRel ) .  
	BIND(URI(CONCAT(STR(?controller),"_",STRAFTER(STR(?workstate),"#"),"_cond")) AS ?condTrIdleRel ) .  
	BIND(URI(CONCAT(STR(?controller),"_",STRAFTER(STR(?workstate),"#"),"_entryAction")) AS ?entryAction ) .  
	BIND(URI(CONCAT(STR(?controller),"_",STRAFTER(STR(?workstate),"#"),"_startOp_1")) AS ?startOp ) .  

}