PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX express: <https://w3id.org/express#> 
PREFIX factory: <https://w3id.org/ontoeng/factory#>
PREFIX osph: <https://w3id.org/ontoeng/osph#>
PREFIX ssn: <http://www.w3.org/ns/ssn/>
PREFIX cs: <https://w3id.org/ontoeng/controlSystem#>
PREFIX ex: <https://w3id.org/ontoeng/expression#>

select distinct ?conwippol ?threshold (group_concat(distinct ?machine) as ?allmachines) (group_concat(distinct ?buffer) as ?allbuffers)
WHERE { 
  ?conwippol rdf:type cs:ConwipControlPolicy . 
  OPTIONAL{
	  ?conwippol cs:hasPolicyExpression ?expr .
	  ?expr ex:hasOperator ex:LESSTHANOREQUAL .  
	  OPTIONAL{
		  ?expr ex:hasRhsOperand/express:hasInteger ?threshold . 
	  }
	  OPTIONAL{
		  ?expr ex:hasLhsOperand ?lhs . 
		  ?lhs rdf:type ex:IteratedBinaryExpression .
		  ?lhs ex:hasOperator ex:ADDITION .
		  OPTIONAL{
			?lhs ex:hasOperands/osph:representsProperty ?prop1 .
			?machine ssn:hasProperty ?prop1 .
			?machine rdf:type/rdfs:subClassOf* factory:MachineTool .
		  }    
		  OPTIONAL{
			?lhs ex:hasOperands/osph:representsProperty ?prop2 .
			?buffer ssn:hasProperty ?prop2 .
			?buffer rdf:type/rdfs:subClassOf* factory:BufferElement .
		  }    
	  }
	}
} 
GROUP BY ?conwippol ?threshold