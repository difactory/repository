PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX ifc: <https://standards.buildingsmart.org/IFC/DEV/IFC4/ADD1/OWL#> 
PREFIX cs: <https://w3id.org/ontoeng/controlSystem#>
PREFIX factory: <https://w3id.org/ontoeng/factory#>
PREFIX ifcext: <https://w3id.org/ontoeng/IFC4_ADD1_extension#>
PREFIX ssn: <http://www.w3.org/ns/ssn/>
PREFIX osph: <https://w3id.org/ontoeng/osph#>

DELETE{
	GRAPH ?g1{ ?name ?b1 ?c1 .}
	GRAPH ?g2{ ?duration ?b2 ?c2 .}
	GRAPH ?g3{ ?prodplan ?b3 ?c3 .}
	GRAPH ?g4{ ?a4 ?b4 ?prodplan .}
	
	GRAPH ?g30 { ?prop ?b30 ?c30 .} 
	GRAPH ?g62 { ?propvalInd ?b62 ?c62 . }
}
WHERE { 
	?prodplan rdf:type factory:ProductionPlan .	
	OPTIONAL{
		?prodplan ifc:name_IfcRoot ?name .
		OPTIONAL{ GRAPH ?g1{ ?name ?b1 ?c1 .} }
	}
	# duration
	OPTIONAL{
		?prodplan ifc:duration_IfcWorkControl ?duration .
		OPTIONAL{ GRAPH ?g2{ ?duration ?b2 ?c2 .} }
	}
	# properties
	OPTIONAL{
		?prodplan ssn:hasProperty ?prop .
		{ 
			GRAPH ?g30{ ?prop ?b30 ?c30 .} 
		}
		UNION{
			?prop osph:hasPropertyValue ?propvalInd .
			{GRAPH ?g62 { ?propvalInd ?b62 ?c62 . }}
		}
	}
	
	OPTIONAL{ GRAPH ?g3{ ?prodplan ?b3 ?c3 .} }
	OPTIONAL{ GRAPH ?g4{ ?a4 ?b4 ?prodplan .} }	
}