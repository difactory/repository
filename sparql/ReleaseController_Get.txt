PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX osph: <https://w3id.org/ontoeng/osph#>
PREFIX fsm: <http://www.learninglab.de/~dolog/fsm/fsm.owl#>
PREFIX ifcext: <https://w3id.org/ontoeng/IFC4_ADD1_extension#>
PREFIX cs: <https://w3id.org/ontoeng/controlSystem#>

SELECT distinct ?controller ?controlledMach ?controlPolicy ?workstate

WHERE { 
  ?controller rdf:type cs:ReleaseController .
  OPTIONAL{
	?controller osph:hasStateMachine ?stM .
	?stM fsm:contains ?region .
	?region fsm:hasStateMachineElement ?relState .
	?relState fsm:Entry/osph:generatesEvent ?event .
	?controlledMach osph:hasStateMachine/fsm:contains/fsm:hasStateMachineElement* ?trIdleWork .
	?trIdleWork fsm:Target ?workstate .
	?trIdleWork fsm:TransitionGuard/fsm:GuardEvent ?eventType .
	?eventType (ifcext:typesObject|ifcext:declaresObject) ?event .
	  
	?relTransition fsm:Target ?relState .
	OPTIONAL{
		?relTransition fsm:TransitionGuard/fsm:GuardCondition/osph:hasConditionExpression ?condexpr .
		?controlPolicy cs:hasPolicyExpression ?condexpr .
	}
  }
}