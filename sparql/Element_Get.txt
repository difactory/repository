PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX factory: <https://w3id.org/ontoeng/factory#>
PREFIX osph: <https://w3id.org/ontoeng/osph#>
PREFIX fsm: <http://www.learninglab.de/~dolog/fsm/fsm.owl#>
PREFIX ifc: <https://standards.buildingsmart.org/IFC/DEV/IFC4/ADD1/OWL#> 
PREFIX ifcext: <https://w3id.org/ontoeng/IFC4_ADD1_extension#>
PREFIX ssn: <http://www.w3.org/ns/ssn/>

select distinct ?elem ?elemclass (group_concat(distinct ?failstate) as ?failstates) (group_concat(distinct ?connTo) as ?connectedTo) (group_concat(distinct ?connFrom) as ?connectedFrom) ?buffCap (count(distinct ?buffrestpos) as ?intBuffCap) ?nServ (group_concat(distinct ?taskclass) as ?taskclasses) (group_concat(distinct ?system) as ?allsys) ?isMachine ?isBuffer
WHERE { 

	?elemclass rdfs:subClassOf* ifc:IfcElement .
	?elem rdf:type ?elemclass . 
	FILTER ( ?elemclass != owl:NamedIndividual ) .
	OPTIONAL{
		{
			?elemclass rdfs:subClassOf* factory:BufferElement .
			BIND("true" AS ?isBuffer ) .
		}
		UNION{
			?elemclass rdfs:subClassOf* factory:MachineTool .
			BIND("true" AS ?isMachine ) .
		}  
	}		
	
	optional{
		?elem ssn:hasProperty ?buffprop .
		?buffprop rdf:type factory:BufferCapacity .
		?buffprop osph:hasPropertySimpleValue ?buffCap .
	}
	optional{
		?elem ssn:hasProperty ?machprop .
		?machprop rdf:type factory:MachineServerN .
		?machprop osph:hasPropertySimpleValue ?nServ .
	}
	optional{
		?elem osph:hasStateMachine ?stm .
		?stm fsm:contains ?failstate .
		?failstate rdf:type/rdfs:subClassOf* factory:MachineToolFailedState . 
	}
	optional{
		?elem ifcext:isConnectedToElement|^ifcext:isConnectedFromElement ?connTo .
	}
	optional{
		?elem ifcext:isConnectedFromElement|^ifcext:isConnectedToElement ?connFrom .
	}
    optional{
      ?elem ifcext:isDecomposedByObject* ?wPos .
      ?wPos rdf:type factory:BufferWorkingPosition .
    }
	optional{
		?elem (ifcext:isDecomposedByObject|^ifcext:decomposesObject)* ?buffrestpos .
		?buffrestpos rdf:type/rdfs:subClassOf* factory:BufferRestingPosition .
	}
    # get tasks assigned to element
    OPTIONAL{ 
      ?elem (ifcext:isDefinedByType|^ifcext:typesObject)*/(ifcext:hasAssignmentTo|^ifcext:hasAssignedObject) ?res .
      ?res (ifcext:isDefinedByType|^ifcext:typesObject)*/(ifcext:hasAssignmentTo|^ifcext:hasAssignedObject) ?task .
      ?res rdf:type/rdfs:subClassOf* factory:ProductionResource .
      ?task rdf:type ?taskclass.
      FILTER ( ?taskclass != owl:NamedIndividual ) .
    }  
	OPTIONAL{
		?sysclass rdfs:subClassOf* factory:TransformationSystem .
		?system rdf:type ?sysclass . 
		?system (ifcext:hasAssignedObject|^ifcext:hasAssignmentTo) ?elem .
		#?system (ifcext:hasAssignedObject|^ifcext:hasAssignmentTo)/(ifcext:isDecomposedByObject|^ifcext:decomposesObject)* ?elem .
	}		
} GROUP BY ?elem ?elemclass ?buffCap ?nServ ?isMachine ?isBuffer