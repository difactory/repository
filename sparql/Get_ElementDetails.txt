PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX factory: <http://www.ontoeng.com/factory#>
PREFIX osph: <http://www.ontoeng.com/osph#>
PREFIX fsm: <http://www.learninglab.de/~dolog/fsm/fsm.owl#>
PREFIX ifcext: <http://www.ontoeng.com/IFC4_ADD1_extension#>

select distinct ?elem ?elemclass (group_concat(distinct ?failstate) as ?failstates) (group_concat(distinct ?conn) as ?connectedTo)
WHERE { 
  # The selected element is a user-defined parameter
  BIND(URI("#element#") AS ?elem) . 	
  ?elem rdf:type ?elemclass . 
  FILTER ( ?elemclass != owl:NamedIndividual ) .
  optional{
	?elem osph:hasStateMachine ?stm .
	?stm fsm:contains ?failstate .
	?failstate rdf:type/rdfs:subClassOf* factory:MachineToolFailedState . 
	}
	optional{
	?elem ifcext:isConnectedToElement|^ifcext:isConnectedFromElement ?conn .
	}
} GROUP BY ?elem ?elemclass