PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX factory: <http://www.ontoeng.com/factory#>
PREFIX osph: <http://www.ontoeng.com/osph#>
PREFIX fsm: <http://www.learninglab.de/~dolog/fsm/fsm.owl#>

SELECT Distinct ?statemach ?state (group_concat(distinct ?target) as ?targets) ?parent ?statetype ?stateclass
 
WHERE {
	?statemach rdf:type fsm:StateMachine .
	OPTIONAL{
		?statemach fsm:contains/fsm:hasStateMachineElement* ?state .
    	?state a ?stateclass .
    	FILTER ( ?stateclass != owl:NamedIndividual ) .
    	{?state rdf:type/rdfs:subClassOf* fsm:Simple . BIND("Simple" as ?statetype)} 
        UNION {?state rdf:type fsm:Composite . BIND("Composite" as ?statetype)} 
		UNION {?state rdf:type fsm:Region . BIND("Region" as ?statetype)} 
        OPTIONAL{
          ?parent fsm:contains|fsm:hasStateMachineElement ?state .
        }
        OPTIONAL{
          ?tr fsm:Source ?state .
          ?tr fsm:Target ?target .
        }
	}
}
GROUP BY ?statemach ?state ?parent ?statetype ?stateclass