PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX osph: <https://w3id.org/ontoeng/osph#>
PREFIX fsm: <http://www.learninglab.de/~dolog/fsm/fsm.owl#>
PREFIX ifc: <https://standards.buildingsmart.org/IFC/DEV/IFC4/ADD1/OWL#> 
PREFIX ifcext: <https://w3id.org/ontoeng/IFC4_ADD1_extension#>
PREFIX cs: <https://w3id.org/ontoeng/controlSystem#>
PREFIX fa: <https://w3id.org/ontoeng/factory#>
PREFIX ssn: <http://www.w3.org/ns/ssn/>

SELECT distinct ?prodsched ?parttype ?part ?partsel ?procplan ?quantity ?duedate ?priority

WHERE { 


	?prodsched rdf:type fa:ProductionSchedule .
	# part / part type
	OPTIONAL{
		?prodsched ifcext:hasAssignedObject|^ifcext:hasAssignmentTo ?partsel .
		?partsel rdf:type/rdfs:subClassOf* fa:ArtifactSelect .
	}  
	# process plan
	OPTIONAL{
		?prodsched ifcext:hasAssignedObject|^ifcext:hasAssignmentTo ?procplan .
		?procplan rdf:type/rdfs:subClassOf* fa:TaskSelect .
	}  
	# quantity
	OPTIONAL{
		OPTIONAL{
			?prodsched ssn:hasProperty ?prop1 .
			?prop1 rdf:type fa:ScheduledQuantity .
			?prop1 osph:hasPropertySimpleValue ?quantity .
		}
		OPTIONAL{
			?prodsched ssn:hasProperty ?prop2 .
			?prop2 rdf:type fa:ScheduledDueDate .
			?prop2 osph:hasPropertySimpleValue ?duedate .
		}
		OPTIONAL{
			?prodsched ssn:hasProperty ?prop3 .
			?prop3 rdf:type fa:ScheduledPriority .
			?prop3 osph:hasPropertySimpleValue ?priority .
		}
	}
  
  	{
		?itsClass rdfs:subClassOf* fa:ArtifactType . 
		?partsel rdf:type ?itsClass .
		BIND(?partsel AS ?parttype ) .
	}
	UNION{
		?itsClass rdfs:subClassOf* fa:Artifact . 
		?partsel rdf:type ?itsClass .
		BIND(?partsel AS ?part ) .
	}

}
