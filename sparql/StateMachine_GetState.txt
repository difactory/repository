PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX factory: <https://w3id.org/ontoeng/factory#>
PREFIX osph: <https://w3id.org/ontoeng/osph#>
PREFIX fsm: <http://www.learninglab.de/~dolog/fsm/fsm.owl#>
PREFIX ifcext: <https://w3id.org/ontoeng/IFC4_ADD1_extension#>
PREFIX cs: <https://w3id.org/ontoeng/controlSystem#>
PREFIX ex: <https://w3id.org/ontoeng/expression#>
PREFIX express: <https://w3id.org/express#> 

SELECT Distinct ?statemach ?state ?doactivity ?doactivityModel ?entryAction ?onentryEvent ?onentryEventModel ?exitAction ?action ?actClass ?actDelay ?actDuration ?actTarget ?actPlace  ?actPos ?actRot ?actVel ?actRepr ?actColor ?actIntensity ?actBlink ?onexitEvent ?onexitEventModel ?parent ?statetype ?stateclass ?outTransition ?transitionTarget ?transitionAction ?trDelay ?event ?cond 
WHERE {
  
	# STATE 	
	?statemach fsm:contains/fsm:hasStateMachineElement* ?state .
	?state rdf:type/rdfs:subClassOf* fsm:State .	
	?state a ?stateclass .
	FILTER ( ?stateclass != owl:NamedIndividual ) .
	
	# STATE TYPE
	{?state rdf:type/rdfs:subClassOf* fsm:Simple . BIND("Simple" as ?statetype)} 
	UNION {?state rdf:type fsm:Composite . BIND("Composite" as ?statetype)} 
	UNION {?state rdf:type fsm:Region . BIND("Region" as ?statetype)} 
	UNION {?state rdf:type fsm:Initial . BIND("Initial" as ?statetype)} 
	UNION {?state rdf:type fsm:Final . BIND("Final" as ?statetype)} 
	
	
	# PARENT (Composite)
	OPTIONAL{ ?parent fsm:hasStateMachineElement ?state . }
	
	# ACTIONS and ACTIVITY
	{
		{ ?state osph:do ?doactivity . 
				OPTIONAL { ?doactivity ifcext:isDefinedByType|^ifcext:typesObject ?doactivityModel} .
				OPTIONAL { ?doactivity ifcext:isDefinedByObject|^ifcext:declaresObject ?doactivityModel} .		
		}
		UNION {
			#{?state fsm:Entry|fsm:Exit ?action .}
			#UNION{
			#	?outTransition fsm:Source ?state . 
			#	?outTransition osph:hasTransitionAction ?action .
			#}	
			
			{ 
				?state fsm:Entry ?action .			
				BIND(?action AS ?entryAction) 				
				OPTIONAL { ?entryAction osph:generatesEvent ?onentryEvent . 
					OPTIONAL { ?onentryEvent ifcext:isDefinedByType|^ifcext:typesObject ?onentryEventModel} .
					OPTIONAL { ?onentryEvent ifcext:isDefinedByObject|^ifcext:declaresObject ?onentryEventModel} .
				}
			}
			UNION{
				?state fsm:Exit ?action .
				BIND(?action AS ?exitAction) 
				OPTIONAL { ?exitAction osph:generatesEvent ?onexitEvent .
					OPTIONAL { ?onexitEvent ifcext:isDefinedByType|^ifcext:typesObject ?onexitEventModel} .
					OPTIONAL { ?onexitEvent ifcext:isDefinedByObject|^ifcext:declaresObject ?onexitEventModel} .
				}
			}
			UNION{
				?outTransition fsm:Source ?state . 
				?outTransition osph:hasTransitionAction ?action .
				BIND(?action AS ?transitionAction)
			}
			
			?action a ?actClass .
			FILTER ( ?actClass != owl:NamedIndividual ) .
			
			OPTIONAL{?action osph:hasActionDelay ?actDelay .}
			OPTIONAL{?action osph:hasActionDuration ?actDuration .}
			OPTIONAL{?action osph:hasActionTarget ?actTarget .}
			
			OPTIONAL{?action osph:hasActionLocation ?actPos .}
			OPTIONAL{?action osph:hasActionRotation ?actRot .}
			
			OPTIONAL{?action osph:hasActionVelocity ?actVel .}			

			OPTIONAL{?action osph:hasActionReprRef ?actRepr .}
			OPTIONAL{?action osph:hasActionReprColor ?actColor .}
			OPTIONAL{?action osph:hasActionReprIntensity ?actIntensity .}		
			OPTIONAL{?action osph:hasActionReprBlinkInterval ?actBlink .}			
		}
	}
	UNION
	{
		# OUTCOMING TRANSITION # ?outTransition ?transitionTarget
		OPTIONAL{ 
		
			?outTransition fsm:Source ?state . 
			?outTransition rdf:type/rdfs:subClassOf* fsm:Transition .
			OPTIONAL{?outTransition fsm:Target ?transitionTarget .}
			
			OPTIONAL{?outTransition osph:hasTransitionAction ?transitionAction .}
			OPTIONAL{?outTransition osph:hasTransitionDelay ?trDelay .}
					
			OPTIONAL{
				?outTransition fsm:TransitionGuard ?guard .
				OPTIONAL{?guard fsm:GuardEvent ?event .}
				OPTIONAL{
					?guard fsm:GuardCondition/osph:hasConditionExpression ?expr .

					# BIN EXPRESSION
					?expr rdf:type ex:BinaryExpression .
					?expr ex:hasOperator/rdfs:label ?operator .  
					# LHS	
					{
						?expr ex:hasLhsOperand ?lhs .
						{
							SELECT  ?expriter ?operator1  (group_concat(distinct STRAFTER(STR(?hs1),"#");SEPARATOR = "," ) as ?alllhs)
							WHERE {
								?expriter rdf:type ex:IteratedBinaryExpression .   
								?expriter ex:hasOperator/rdfs:label ?operator1 .  
								?expriter ex:hasOperands/(osph:representsProperty|osph:isRepresentedByVariable) ?hs1 .
							} GROUP BY ?expriter ?operator1     
						}
						FILTER(?lhs=?expriter ) .
						BIND(REPLACE(?alllhs,",",?operator1) AS ?lhsstr) 
					}
					UNION{
						?expr ex:hasLhsOperand ?lhs .
						?lhs rdf:type/rdfs:subClassOf* ex:Constant .  
						?lhs express:hasInteger|express:hasDouble|express:hasString ?lhsstr .
					}  
					
					# RHS
					{
						?expr ex:hasRhsOperand ?rhs .
						{
							SELECT  ?expriter2 ?operator2  (group_concat(distinct STRAFTER(STR(?hs2),"#");SEPARATOR = "," ) as ?allrhs)
							WHERE {
								?expriter2 rdf:type ex:IteratedBinaryExpression .   
								?expriter2 ex:hasOperator/rdfs:label ?operator2 .  
								?expriter2 ex:hasOperands/(osph:representsProperty|osph:isRepresentedByVariable) ?hs2 .
							} GROUP BY ?expriter2 ?operator2     
						}
						FILTER(?rhs=?expriter2 ) .
						BIND(REPLACE(?allrhs,",",?operator2) AS ?rhsstr) 
					}
					UNION{
						?expr ex:hasRhsOperand ?rhs .
						?rhs rdf:type/rdfs:subClassOf* ex:Constant .  
						?rhs express:hasInteger|express:hasDouble|express:hasString ?rhsstr .
					}    

					BIND(CONCAT(?lhsstr,STR(?operator),STR(?rhsstr)) AS ?cond)			
				}      
			}
		}
	}
}