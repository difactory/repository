PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX osph: <https://w3id.org/ontoeng/osph#>
PREFIX fsm: <http://www.learninglab.de/~dolog/fsm/fsm.owl#>
PREFIX ifc: <https://standards.buildingsmart.org/IFC/DEV/IFC4/ADD1/OWL#> 
PREFIX ifcext: <https://w3id.org/ontoeng/IFC4_ADD1_extension#>
PREFIX cs: <https://w3id.org/ontoeng/controlSystem#>
PREFIX fa: <https://w3id.org/ontoeng/factory#>
PREFIX ssn: <http://www.w3.org/ns/ssn/>
PREFIX fa: <https://w3id.org/ontoeng/factory#>

SELECT distinct ?prodplan ?time (group_concat(distinct ?sys) as ?allsys) (group_concat(distinct ?contr) as ?allcontr) (group_concat(distinct ?prodsched) as ?allprodsched) (group_concat(distinct ?prodhist) as ?allprodhist)

WHERE { 
  ?prodplan rdf:type fa:ProductionPlan .
  # duration
  OPTIONAL{
	{?prodplan ifc:duration_IfcWorkControl ?time .}
	UNION{
		?prodplan ssn:hasProperty ?time .
		?time a fa:ExpectedDuration .
	}
  }
  # systems
  OPTIONAL{
	?prodplan ifcext:hasAssignedObject|^ifcext:hasAssignmentTo ?sys .
	?sys rdf:type/rdfs:subClassOf* fa:TransformationSystem .
  }  
  # controllers
  OPTIONAL{
	?prodplan cs:hasExecutionController ?contr .
	?contr rdf:type/rdfs:subClassOf* cs:ControllerFB .
  }
  # production batches
  OPTIONAL{
	?prodplan ifcext:isDecomposedByObject|^ifcext:decomposesObject ?prodsched .
	?prodsched rdf:type/rdfs:subClassOf* fa:ProductionSchedule .
  }
  # production histories
  OPTIONAL{
	?prodplan osph:hasHistory ?prodhist .
	?prodhist rdf:type/rdfs:subClassOf* fa:ProductionPlanHistory .
  }
}
GROUP BY ?prodplan ?time